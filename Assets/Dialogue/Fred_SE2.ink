INCLUDE globals.ink
EXTERNAL StartTTMicro()
->Start 

==Start==
~convo_numberF=3
Fred! Good to see you stop by again my friend, what can I get ya today? #speaker:Graciana #mood:happy

Hi Graciana! Orange swirl please #speaker:Fred #mood:happy
 
Coming right up! #speaker:Graciana #mood:happy
(It's adorable we gave his "shakes" nicknames like orange swirl, when it's just me stirring up an orange lava lamp a few times before I give it to him.) #speaker:Graciana #mood:neutral

Oh! By the way, Graciana! I have a hundred questions to ask you! #speaker:Fred #mood:pump

Oh? So many? What's got your attention this time around? #speaker:Graciana #mood:think

Cartoons! #speaker:Fred #mood:happy

Oh? Got a new interest in them? #speaker:Graciana #mood:think

Yes, and no. #speaker:Fred #mood:neutral
Frank really likes them, and he mentions them sometimes when we're working! #speaker:Fred #mood:happy
I really want to watch a few of them, but I don’t know which one he like the best. #speaker:Fred #mood:neutral

*[How thoughtful!]
//Good
~affectionF=affectionF+4
Aww, that's so thoughtful of you! What are the shows? #speaker:Graciana #mood:happy #sfx:GainedPoint

I just wanna have something to talk about with him besides work, and he really likes these shows! #speaker:Fred #mood:happy
Well, I think he does, with how much he talks about them. #speaker:Fred #mood:neutral

That's great, Fred! I'm sure he would love to! #speaker:Graciana #mood:neutral

Well, there are a few, but he talks about these three the most. #speaker:Fred #mood:neutral


-> Cartoons

*[Pick one & wing it!]
//Chaotic
~chaosF=chaosF+2
Why don’t you just pick one at random and wing it? #speaker:Graciana #mood:think #sfx:CosmicPoint

But what if I chose the wrong one? #speaker:Fred #mood:sad

Well he talked about multiple right? #speaker:Graciana #mood:think

Yeah! #speaker:Fred #mood:happy

Then that means he probably likes each one of them in their own way, so it makes sense that he wouldn't mind talking about any one of them. #speaker:Graciana #mood:neutral
Just chose whichever one sounds the most interesting, and bring it up and see if he likes it! #speaker:Graciana #mood:happy

And... If he doesn't like any of them? #speaker:Fred #mood:sad

Then you can still talk to me about them over a orange swirl anytime! #speaker:Graciana #mood:neutral

Well, which one do you think sounds interesting? #speaker:Fred #mood:galaxy
->Cartoons

*[Watch what *you* want]
//Bad
~affectionF=affectionF-3
You know, you can watch anything you want, not just Franks' shows, right? #speaker:Graciana #mood:think #sfx:LostPoint

I know... #speaker:Fred #mood:sad
But I still want to watch them... Don't you think Frank would like it if I watched them? #speaker:Fred #mood:neutral

He might, but he might just be telling you about them to try and figure out what you really like instead. #speaker:Graciana #mood:neutral
Either way, if you're going to do something for Frank you should have a reason why. Even though he may be nice to you at work, doesn't mean you have to do everything for him. #speaker:Graciana #mood:neutral

...I don't get any of what you just said... #speaker:Fred #mood:neutral

Think about it like this... #speaker:Graciana #mood:neutral
If you tooted in the same port you drank your shakes through, wouldn't that then make the shakes taste worse? #speaker:Graciana #mood:neutral

Duh, the suit has different ends for that! #speaker:Fred #mood:galaxy

Exactly! Frank is one end of your life during work, and you can find another end for when you're off work! #speaker:Graciana #mood:neutral
Right? #speaker:Graciana #mood:think
 
...My head hurts #speaker:Fred #mood:sad

From the explanation, or a brainfreeze? #speaker:Graciana #mood:think

... #speaker:Fred #mood:neutral

... #speaker:Graciana #mood:neutral
 
Yea. #speaker:Fred #mood:Happy

Let's enjoy the swirl and worry about that all later, huh? #speaker:Graciana #mood:neutral
But I'm curious, what were the shows Frank talked about? #speaker:Graciana #mood:think

Well, there were a few. #speaker:Fred #mood:neutral
->Cartoons


==Cartoons==

There's The Adventures of Liquid Boy and Flammable Girl, Ladel Heads' Bizzare Tale, and Ra, Vi, & Har, The Last Full Senders! #speaker:Fred #mood:neutral

*[Liquid Boy and Flammable Girl]

Oh hey, that one sounds familiar! I think I might've watched something similar when I was a kid. #speaker:Graciana #mood:think
Tell me a bit about it! #speaker:Graciana #mood:think

Well, there's this boy who loves this girl, but they can never be together because everytime he tries to see her a strange puzzle shows up, and they can only be together for a few seconds before more puzzle keeps appearing. #speaker:Fred #mood:neutral
I don't think I've seen them together for more than a few minutes at most #speaker:Fred #mood:neutral

Damn, kids shows have gotten dark... #speaker:Graciana #mood:sad
Mine just used to be about kids teaming up together stop evil adults with martial arts. #speaker:Graciana #mood:neutral

That's not dark? #speaker:Fred #mood:Happy

Touché Fred, but I think if you like it and Frank likes it, there's plenty of reason to go with it as your bonding show to talk about together. #speaker:Graciana #mood:neutral
Just make sure to ask him questions about it like what he liked about it, or what interested him about certain characters.
People love to talk about their passions. #speaker:Graciana #mood:happy

Okay! #speaker:Fred #mood:pump
What should I do besides that? #speaker:Fred #mood:neutral

Be your curious little self, and I'm sure anything you think of to ask or say he'll be happy to listen and chat along! #speaker:Graciana #mood:neutral

But on that note... I do have another question for you, Fred. #speaker:Graciana #mood:think

->TheTalk

*[Ladel Heads Bizzare Tale] 

Okay, now this one I have definitely not heard of before. #speaker:Graciana #mood:neutral

It's kind of creepy, but like... interesting creepy. #speaker:Fred #mood:neutral
 
A man with a ladel head telling bizzare stories is the definition of creepy... #speaker:Graciana #mood:think 
But if he likes it and you're fine with it, go for it! #speaker:Graciana #mood:neutral

Yeah! I'm sure it's actually not that strange anyway! #speaker:Fred #mood:pemp
As long as Frank likes it, I'll give myself nightmares for a few nights! #speaker:Fred #mood:happy

Atta boy, now that's commitment #speaker:Graciana #mood:neutral
Speaking of which... #speaker:Graciana #mood:think

->TheTalk

*[Ra, Vi, and Har The Last Full Senders]

That seems a little cheesy, no? #speaker:Graciana #mood:think
Sounds like a direct rip off of a show I used to watch when I was a kid. #speaker:Graciana #mood:neutral

I mean it looks good... I like it! #speaker:Fred #mood:happy

Fair enough... #speaker:Graciana #mood:think
Well if you both like it, I don't see why not to go with this one! #speaker:Graciana #mood:neutral

I'm not sure why he would still like it as his age, but I would watch this one by myself too!  #speaker:Fred #mood:Happy

Oh? Invested, are we? #speaker:Graciana #mood:happy
You'll have to let me know your rating after you finish! #speaker:Graciana #mood:happy

I will! I'll ask Frank for his too! #speaker:Fred #mood:pump

Ah, that leads me to my next question... #speaker:Graciana #mood:think

->TheTalk



==TheTalk==

So, Fred, I have been meaning to ask... #speaker:Graciana #mood:neutral

You, uh, don't have parents, do you...? #speaker:Graciana #mood:think

... #speaker:Fred #mood:neutral

... #speaker:Graciana #mood:neutral

I'm sorry, it's a hard question, but I only hear you talking about your friends and coworkers and living on the UMS carrier, but never anything about anyone that takes care of you besides Frank. #speaker:Graciana #mood:neutral
Which is great that Frank means so much to you, but I just wanted to make sure I know that this is me helping you with father-son bonding, versus friends just hanging out.  #speaker:Graciana #mood:neutral
 
... #speaker:Fred #mood:sad
It's what you said, I never met my parents. #speaker:Fred #mood:neutral
I don't even know if I have parents... #speaker:Fred #mood:sad
My most recent memories are of when I entered this UMS suit. #speaker:Fred #mood:neutral
But ever since then, Frank has taken care of me in being someone to talk to, and showing me how to do work while also having fun meeting people!  #speaker:Fred #mood:happy
He's the closest thing I've had to a dad, or any family really... #speaker:Fred #mood:sad
 
*[A dad like him?]
~affectionF=affectionF+4

And do you want a father like him? #speaker:Graciana #mood:think #sfx:GainedPoint

I mean... I never really thought about it too hard... #speaker:Fred #mood:neutral
But I guess I wouldn't mind it! #speaker:Fred #mood:happy
He's kind of a nerd, and a little weird, and is constantly working... #speaker:Fred #mood:neutral
But I like him anyways! #speaker:Fred #mood:happy

Have you thought about asking him to adopt you? #speaker:Graciana #mood:think

What would that do? #speaker:Fred #mood:neutral

Well, he could be your dad! Spend more time with you, take care of you, help you more outside of work, and maybe help you fight the whole "you working for another 100 years" deal!  #speaker:Graciana #mood:neutral

Oh... I didn't think about that. #speaker:Fred #mood:neutral

I just like spending time with him! #speaker:Fred #mood:happy

And that's great Fred! From what you tell me he sounds like he's a great Eldritch UMS worker, and he helps you out so much already! So he might be open to it if you ask!  #speaker:Graciana #mood:neutral

And what's the worst thing he says? #speaker:Fred #mood:neutral
I should leave forever? #speaker:Fred #mood:sad

(Oh Gods...) #speaker:Graciana #mood:sad
Hm... #speaker:Graciana #mood:think
I was thinking the word no, but I would hope if he did ever say that to you, you would knock out his knees and never look back, as he would be 100% wrong!  #speaker:Graciana #mood:happy

You want me to kick Franks knees? #speaker:Fred #mood:neutral

Only if he hurts your feelings, or says anything you strongly disagree with! A good kick never hurt someone with an incorrect opinion!  #speaker:Graciana #mood:think

(Damn, guess my inner Newer City is coming out...)  #speaker:Graciana #mood:neutral
But don't worry about that for now, focus on asking him next time you guys do something together!
If he does say yes, imagine all the exicting things you could do together as father-son! #speaker:Graciana #mood:happy

I'll try! #speaker:Fred #mood:happy

That's all I ask you do, little star. #speaker:Graciana #mood:neutral

Now, I can feel one of Chef Swatts million eyes staring at me not doing work, so let's get this table cleaned up and you back to your ship, huh?  #speaker:Graciana #mood:think

Alright, thanks Graciana! #speaker:Fred #mood:happy
->Final

*[A mother figure?]
~chaosF=chaosF+2

Have you ever had a mother-like figure in your life? #speaker:Graciana #mood:think #sfx:CosmicPoint

Uhh... #speaker:Fred #mood:neutral
Not really! #speaker:Fred #mood:happy
One of the older UMS workers yells at me a lot for talking too much on breaks, but that's about it. #speaker:Fred #mood:neutral

(Poor little star, never had parents growing up besides a strange Eldritch UMS worker who he seems to like, for some reason...) #speaker:Graciana #mood:think
 
And do you ever think about what it would be like to live with a family? #speaker:Graciana #mood:think

Sometimes, whenever I see them in cartoons and stuff! #speaker:Fred #mood:happy

And you don't really have a mother figure at all? No nice UMS ladies who try to take care of you or do anything for you? #speaker:Graciana #mood:think

Most of them don't really talk to me because they're afraid of me being made of a star... #speaker:Fred #mood:sad
They think I'll go boom or something just asking them how their day was...

And you don't, right? #speaker:Graciana #mood:think

Not yet! #speaker:Fred #mood:angry

Well shame on them, then! #speaker:Graciana #mood:happy

(Earth to Graciana... You know what you're thinking, now is not the time for you to adopt a child...)
(Especially one who you just met a few days ago.) #speaker:Graciana #mood:think
(Counter point, he's adorable and being forced to work for a living.) 
(I just got a job and have somewhat stable income to help take care of him and myself...) #speaker:Graciana #mood:neutral
(Counter counter point, it would be weird if I just straight up asked him if he wanted me to adopt him...) #speaker:Graciana #mood:think
 
Are you okay? You've been staring off into space for a while. #speaker:Fred #mood:neutral
 
Heh, funny, because I was actually staring into space, since, y'know, the window, to space, behind us... #speaker:Graciana #mood:neutral
 
... #speaker:Fred #mood:neutral

... #speaker:Graciana #mood:neutral

... #speaker:Fred #mood:neutral

You can pretend to laugh at my bad jokes, at the very least. #speaker:Graciana #mood:sad
 
That was really funny! #speaker:Fred #mood:pump

Too late... #speaker:Graciana #mood:neutral
But I appreciate the effort, Fred.

Now your break is about to end, and I gotta go back to doing my job before I'm fired, so let's go little star. #speaker:Graciana #mood:happy
->Final

*[Do you want a family?]
~affectionF=affectionF-3

Are you sure a work family is the real family you're looking for? #speaker:Graciana #mood:think #sfx:LostPoint

I'm not really sure... #speaker:Fred #mood:neutral
He's nice, and he does nice things for me...
But I've never had a family before, so maybe I guess? #speaker:Fred #mood:happy

Well, one of my good friends left me with a saying a while ago... #speaker:Graciana #mood:think
Never drop an asteroid where you eat #speaker:Graciana #mood:neutral

Why would you do that? #speaker:Fred #mood:neutral
Actually how could you drop an Asteroid?

They're in space. #speaker:Graciana #mood:neutral

Oh yeah, I should've seen that one coming... #speaker:Fred #mood:neutral

The important thing is this, little star, you already work together, live in the same ship, watch TV shows together, and do the same delivery routes, right?  #speaker:Graciana #mood:think

Yeah! #speaker:Fred #mood:pump

Well, if you already do all that stuff, it sounds like you're pretty close already, right? #speaker:Graciana #mood:think
 
I guess... #speaker:Fred #mood:neutral

If you're already close, then maybe you should look for someone to help take care of you outside of work? #speaker:Graciana #mood:think

Hm... #speaker:Fred #mood:neutral
I don't know if I get it.

I got it... 
Do you have anyone else who you really admire or like, who isn't from work? #speaker:Graciana #mood:think

... #speaker:Fred #mood:neutral
 
Oh hun... I wasn't thinking me...  #speaker:Graciana #mood:sad

... #speaker:Fred #mood:neutral

Oh no, I was just taking a second to think, yeah I think there's a few, I think. #speaker:Fred #mood:happy
There's only like two people I really know outside of work...

Well there you go! Maybe look into how well you get along with them and see if they would be willing to hang out with you, and see how much you like them versus how much you like Frank! #speaker:Graciana #mood:happy

Okay! #speaker:Fred #mood:galaxy

Alright bud, I have to go clean up, but have a nice day at work and update me on how things go on your next break, okay? #speaker:Graciana #mood:neutral

Alright! Bye Graciana! #speaker:Fred #mood:happy

Bye, my favorite little star! #speaker:Graciana #mood:happy



->Final



==Final==

(Fred waves at me, as he exits the Diner.) #speaker:Graciana #mood:neutral #ss:Fred:blank
Alright... Time to see what else there is to do around here... #speaker:Graciana #mood:think #scg:black
...

Hey, Graciana, can you come help me with this trash? The black hole appeared again! #speaker:CeCe #mood:neutral #ecg:
Got it... #speaker:Graciana #mood:neutral
(I still don't know if I should be avoiding it or not...) #speaker:Graciana #mood:think

~TTMLevel=3
~timeOfDay="afternoon"
~currentConvo="GMG3"
~QuickSave()
~StartTTMicro()
    -> DONE
